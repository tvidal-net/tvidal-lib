buildscript {
    dependencies {
        classpath group: 'org.jetbrains.kotlin',
                name: 'kotlin-gradle-plugin',
                version: '1.3.21'
    }
}

plugins {
    id 'idea'
}

allprojects {
    group = 'uk.tvidal'
    version = '0.1-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    dependencies {
        api group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'
        api group: 'org.jetbrains.kotlin', name: 'kotlin-reflect'

        testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.4.0'
        testImplementation group: 'io.mockk', name: 'mockk', version: '1.9.1'
    }

    archivesBaseName = "tvidal-${project.name}"

    jar {
        manifest {
            attributes(
                    'Implementation-Group': project.group,
                    'Implementation-Title': archivesBaseName,
                    'Implementation-Version': project.version
            )
        }

        zip64 true
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    assemble {
        dependsOn sourcesJar
    }

    publishing {
        publications {
            maven(MavenPublication) {
                artifactId = archivesBaseName
                from components.java
                artifact sourcesJar

                pom {
                    url = 'https://github.com/tvidal-net/tvidal-lib'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'tvidal-net'
                            name = 'Thiago Vidal'
                            email = 'thiago@tvidal.uk'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/tvidal-net:tvidal-lib.git'
                        developerConnection = 'scm:git:ssh://github.com/tvidal-net:tvidal-lib.git'
                        url = 'https://github.com/tvidal-net/tvidal-lib'
                    }
                }
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }
}