plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'idea'
}

allprojects {
    group = 'uk.tvidal'
    version = '0.1-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = sourceCompatibility
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    dependencies {
        api group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', force: true
        api group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', force: true

        testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.4.0'
        testImplementation group: 'io.mockk', name: 'mockk', version: '1.9.1'
    }

    archivesBaseName = "tvidal-${project.name}"

    test {
        useJUnitPlatform()
    }

    jar {
        manifest {
            attributes(
                    'Implementation-Group': project.group,
                    'Implementation-Title': archivesBaseName,
                    'Implementation-Version': project.version
            )
        }

        zip64 true
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    assemble {
        dependsOn sourcesJar
    }

    publishing {
        publications {
            maven(MavenPublication) {
                artifactId = archivesBaseName
                from components.java
                artifact sourcesJar

                pom {
                    url = 'https://github.com/tvidal-net/tvidal-lib'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'tvidal-net'
                            name = 'Thiago Vidal'
                            email = 'thiago@tvidal.uk'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/tvidal-net:tvidal-lib.git'
                        developerConnection = 'scm:git:ssh://github.com/tvidal-net:tvidal-lib.git'
                        url = 'https://github.com/tvidal-net/tvidal-lib'
                    }
                }
            }
        }
    }

    task install {
        dependsOn publishToMavenLocal
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }
}
